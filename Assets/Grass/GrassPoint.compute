// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel FindGrassPoints
#pragma kernel FindGrassCount

#include "Simplex.compute"
#include "UnityCG.cginc"
#include "Scale.compute"

// Struct for GrassData with position and UV's
struct GrassData {
    float4 position;
    float2 uv;
    float displacement;
    uint placePosition;
};

// Initialize variables
AppendStructuredBuffer<GrassData> _GrassBuffer;
RWStructuredBuffer<uint> _ChunkCounter;

Texture2D<float4> _HeightMap;
Texture2D<float4> _PlacementMap;

SamplerState sampler_HeightMap;
SamplerState sampler_PlacementMap;

float3 _TerrainPosition;
float2 _TerrainResolution;
float _MaxTerrainHeight, _Scale, _MeshHeight;

int _HeightMapRes, _TerrainDim, _XOffset, _YOffset, _NumChunks, _ChunkDimension;

// Function to get UV coordinates of current id
float2 GetUVTexCoord(uint3 id : SV_DispatchThreadID) {
    float2 uv = 0.0f;

    // Convert pixel coordinates to normalized value (0-1) and use that to sample placement map and get position for grass
    uv.x = id.x + _ChunkDimension * _XOffset;
    uv.y = id.y + _ChunkDimension * _YOffset;

    uv = normalize2D(uv, _TerrainResolution);

    return uv;
}

// Check if pixel parameter is valid color
bool IsPixelValid(float4 pixel) {
    float4 black = float4(0.0, 0.0, 0.0, 1.0);
    return (pixel.r == black.r && pixel.g == black.g && pixel.b == black.b && pixel.a == black.a);
}

// 8x8x1 threads which makes for optimal 64 threads
[numthreads(8, 8, 1)]
void FindGrassPoints (uint3 id : SV_DispatchThreadID)
{
    // Check if id.x and id.y is within the placement map resolution range
    if (id.x < uint(_ChunkDimension) && id.y < uint(_ChunkDimension)) {
        // Load pixel data from placement map
        float2 pixelUV = float2(GetUVTexCoord(id));
        float4 pixel = _PlacementMap.SampleLevel(sampler_PlacementMap, pixelUV, 0);

        // Initialize a grass data variable
        GrassData grass;

        // Check if the pixel is black
        if (IsPixelValid(pixel)) {
            // Set the grass position and cast the chunk dimension to float
            float4 pos = 0.0f;
            float chunkDimension = float(_ChunkDimension);

            // Set XZ position of grass
            pos.x = (id.x - (chunkDimension * 0.5f * _NumChunks)) + chunkDimension * _XOffset;
            pos.z = (id.y - (chunkDimension * 0.5f * _NumChunks)) + chunkDimension * _YOffset;
            pos.xz *= (1.0f / _Scale);

            // Apply noise to the horizontal position coordinates of the grass
            pos.x += snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 3.0f) * 0.3f;
            pos.z += snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 4.0f) * 0.3f;

            // Find Y displacement of grass by sampling height map
            float4 displacement = _HeightMap.SampleLevel(sampler_HeightMap, pixelUV, 0);
            pos.y = displacement.r * _MaxTerrainHeight * 2;

            // Get a noise value based of the x and z coordinates
            float noise = abs(snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 2.2f));

            // Set tallness of grass
            pos.w = lerp(0.3f, 0.8f, noise);

            // Apply calculated data to grass object
            grass.position = pos;
            grass.displacement = displacement.r * _MaxTerrainHeight * 2;
            grass.uv = pixelUV;
            grass.placePosition = 1;

            // Update grass in the buffer
            _GrassBuffer.Append(grass);
        } 
    }
}

// Function to find number of actual grass per chunk
[numthreads(8, 8, 1)]
void FindGrassCount (uint3 id: SV_DispatchThreadID)
{
    if (id.x < uint(_ChunkDimension) && id.y < uint(_ChunkDimension)) {
        float2 pixelUV = float2(GetUVTexCoord(id));
        float4 pixel = _PlacementMap.SampleLevel(sampler_PlacementMap, pixelUV, 0);

        if (IsPixelValid(pixel)) {
            InterlockedAdd(_ChunkCounter[0], 1);
        }
    }
}
